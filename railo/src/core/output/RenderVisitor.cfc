import craft.core.content.Composite;
import craft.core.content.Document;
import craft.core.content.Layout;
import craft.core.content.Leaf;
import craft.core.content.Placeholder;
import craft.core.content.Section;
import craft.core.content.Visitor;

import craft.core.request.Context;

component implements="Visitor" {

	public void function init(required Context context) {

		variables._context = arguments.context

		// Define state. The following state variables will be modified during component traversal.
		// The sections in Document instances are kept, so that Placeholder instances can pick them up.
		variables._sections = {}
		// Generated content. At the end of the process, this variable contains the request content.
		variables._content = null
		// Content generated by child components. This content is converted to the appropriate content type.
		variables._contents = []

	}

	public Any function content() {
		return variables._content
	}

	public void function visitLayout(required Layout layout) {
		arguments.layout.section().accept(this)
	}

	public void function visitDocument(required Document document) {

		// Pick up the sections / placeholders that this document is filling.
		variables._sections.append(arguments.document.sections())

		arguments.document.layout().accept(this)
	}

	public void function visitLeaf(required Leaf leaf) {

		var model = arguments.leaf.model(variables._context)
		var view = arguments.leaf.view(variables._context)

		variables._content = view.render(model, variables._context.requestMethod())
		variables._contents.append(variables._content)

	}

	public void function visitComposite(required Composite composite) {

		// Copy state in local variables.
		var contents = variables._contents

		var model = arguments.composite.model(variables._context, variables._model)
		var view = arguments.composite.view(variables._context)

		// Overwrite state.
		variables._contents = []

		// During traversal, the contents of the children will be appended to the _contents array.
		arguments.composite.traverse(this)

		// Put the content on the model so the view can include it.
		model.__content__ = variables._contents

		variables._content = view.render(model, variables._context.requestMethod())

		// Revert state.
		variables._contents = contents

		variables._contents.append(variables._content)

	}

	public void function visitPlaceholder(required Placeholder placeholder) {

		// The placeholder is filled if its ref exists as a key in the sections struct.
		var ref = arguments.placeholder.ref()

		if (variables._sections.keyExists(ref)) {
			variables._sections[ref].accept(this)
		}

	}

	public void function visitSection(required Section section) {
		arguments.section.traverse(this)
	}

}